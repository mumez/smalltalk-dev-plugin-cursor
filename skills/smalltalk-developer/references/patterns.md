# Smalltalk Implementation Patterns

Useful implementation patterns.

## Singleton

Use when exactly one instance of a class should exist for the lifetime of the image (e.g., registries, connection pools, application-wide services).

- Use a **class instance variable** (not a class variable, which would be shared with subclasses).

```smalltalk
Class {
	#name : 'MySingleton',
	#superclass : 'Object',
	#instVars : [],
	#classInstVars : [
		'current'
	],
	...
}
```

- Name the class instance variable `current`, `default`, or `soleInstance`.

- Define an `initialize` class method to reset the variable (to `nil`).

```smalltalk
{ #category : 'class initialization' }
MySingleton class >> initialize [
	current := nil.
]
```

- Define a lazy initialization accessor to return the singleton instance.

```smalltalk
{ #category : 'accessing' }
MySingleton class >> current [
	^ current ifNil: [ current := self new ]
]
```

## Settings

Use when a component needs configurable parameters with safe access, default values, and the ability to copy/customize a baseline configuration (e.g., server settings, client options).

- Do **not** use a bare `Dictionary` for settings — it accepts any key/value and has no encapsulation.
- Define a dedicated Settings class with a dictionary-like interface. This enables safe access and lazily initialized default values.

```smalltalk
Class {
	#name : 'MySettings',
	#superclass : 'Object',
	#instVars : [
		'settingsDict'
	],
	#classInstVars : [
		'default'
	],
	...
}
```

- Expose dictionary-like primitives internally (`actions-dictionary` category).

```smalltalk
{ #category : 'actions-dictionary' }
MySettings >> at: key ifAbsent: aBlock [
	^ self settingsDict at: key ifAbsent: aBlock
]

{ #category : 'actions-dictionary' }
MySettings >> at: key ifAbsentPut: aBlock [
	^ self settingsDict at: key ifAbsentPut: aBlock
]

{ #category : 'actions-dictionary' }
MySettings >> at: key put: value [
	^ self settingsDict at: key put: value
]

{ #category : 'actions-dictionary' }
MySettings >> keys [
	^ self settingsDict keys
]
```

- Define typed accessors for each setting. 

```smalltalk
{ #category : 'accessing' }
MySettings >> port [
	^ self at: #port ifAbsent: [ self defaultPort ]
]

{ #category : 'accessing' }
MySettings >> port: aNumber [
	self at: #port put: aNumber
]
```

- Define default value accessor

```smalltalk
{ #category : 'defaults' }
MySettings >> defaultPort [
	^ 8081
]
```

- Getter may use lazy initialization via `ifAbsentPut:` for caching computationally heavy values.

```smalltalk
{ #category : 'accessing' }
MySettings >> serverKey [
	^ self at: #serverKey ifAbsentPut: [ self computeServerKey ]
]
```


- Define `asDictionary` for interoperability (legacy interfaces, JSON serialization, etc.).

```smalltalk
{ #category : 'converting' }
MySettings >> allKeys [
	^ (self class selectorsInProtocol: 'accessing') select: [ :each | each isUnary ].
]
```

- This will export all setting values as a dictionary 

```smalltalk
{ #category : 'converting' }
MySettings >> asDictionary [
	^ self allKeys inject: Dictionary new into: [ :dict :key |
		dict at: key put: (self perform: key);
		yourself ]
]
```

- If you would like to omit unset values, you can use #keys instead 

```smalltalk
{ #category : 'converting' }
MySettings >> asRpcDictionary [
	^ self keys inject: Dictionary new into: [ :dict :key |
		dict at: key put: (self perform: key);
		yourself ]
]
```

- Define `defaultCopied` to obtain a customizable copy of the default instance. The original default is not affected.

```smalltalk
{ #category : 'instance creation' }
MySettings class >> defaultCopied [
	^ self new initFrom: self default
]

{ #category : 'initialization' }
MySettings >> initFrom: otherSettings [
	otherSettings settingsDict keysAndValuesDo: [ :k :v |
		self settingsDict at: k put: v ]
]
```


## Options builder

Use when a method accepts a parameter object (e.g., `RsSearchOptions`) and you want to avoid forcing the caller to instantiate that class directly. A builder block keeps call sites concise and decouples them from the options class name.

Suppose we have a method:
`RsSearcher >> search: indexName query: query options: options`
where `options` is an `RsSearchOptions` instance.

Without the pattern, the caller must know and instantiate `RsSearchOptions` explicitly:

```smalltalk
| options |
options := RsSearchOptions new.
options offset: 10; limit: 20; returnFields: #('score' 'values').
searcher search: 'idx' query: 'st*' options: options
```

Add a companion method that accepts a builder block instead:

```smalltalk
searcher search: 'idx' query: 'st*' optionBy: [ :opts |
	opts offset: 10; limit: 20; returnFields: #('score' 'values') ]
```

`RsSearcher` delegates to the original method after building the options:

```smalltalk
{ #category : 'actions' }
RsSearcher >> search: indexName query: query optionBy: aBlock [
	| options |
	options := RsSearchOptions in: aBlock.
	^ self search: indexName query: query options: options
]
```

`RsSearchOptions` provides an `in:` class-side factory that evaluates the block:

```smalltalk
{ #category : 'instance creation' }
RsSearchOptions class >> in: aBuilderBlock [
	| instance |
	instance := self new.
	aBuilderBlock value: instance.
	^ instance
]
```

The caller no longer needs to reference `RsSearchOptions` directly. The interface is more fluent and type-safe — passing an unrelated options object becomes impossible.

## Immutable return object wrapper

Use when an API returns a `Dictionary` with a well-defined structure (e.g., a parsed JSON response). Instead of handing the raw dictionary to clients, wrap it in a dedicated class.

- Do **not** mechanically generate an accessor/mutator pair for every field. Design the wrapper with immutability in mind.
- Accessors make the shape and types of the return value explicit.
- Omitting public mutators signals that the return value is read-only.

### Bad example

```smalltalk
Class {
	#name : 'XxDescription',
	#superclass : 'Object',
	#instVars : [
		'label',
		'properties',
		'types',
		'options',
		...
	],
	...
}

{ #category : 'instance creation' }
XxDescription class >> fromDictionary: aDictionary [
	^ self new
		label: (aDictionary at: 'label' ifAbsent: []);
		properties: (aDictionary at: 'properties' ifAbsent: []);
		types: (aDictionary at: 'types' ifAbsent: []);
		options: (aDictionary at: 'options' ifAbsent: []);
		...
		yourself
]
```

Clients see both `label` and `label:` in the `accessing` category, so nothing stops them from overwriting values after the fact — even though `XxDescription` is meant to be a read-only return value.

This design is also fragile when the API adds a new field: three places must change (`fromDictionary:`, the accessor, and the mutator).

### Good example

```smalltalk
Class {
	#name : 'XxDescription',
	#superclass : 'Object',
	#instVars : [
		'rawDescription'
	],
	...
}

{ #category : 'instance creation' }
XxDescription class >> fromDictionary: aDictionary [
	^ self new
		rawDescription: aDictionary;
		yourself
]

{ #category : 'accessing' }
XxDescription >> label [
	^ self rawDescription at: 'label' ifAbsent: []
]

{ #category : 'private-accessing' }
XxDescription >> rawDescription [
	^ rawDescription
]

{ #category : 'private-accessing' }
XxDescription >> rawDescription: aDictionary [
	rawDescription := aDictionary
]
```

Clients see only `label` in the `accessing` category. The only accessor/mutator pair is `rawDescription` / `rawDescription:`. Smalltalk cannot make `rawDescription:` truly private, but this design states immutability much more clearly. Placing the pair in a `private-accessing` category further signals that it must not be set from outside.

Supporting a new field only requires adding one accessor.
